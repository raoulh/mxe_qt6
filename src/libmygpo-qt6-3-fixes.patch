From a83a5ed346d96d2f26ce7563480245dc9a963af3 Mon Sep 17 00:00:00 2001
From: Jonas Kvinge <jonas@jkvinge.net>
Date: Wed, 4 Aug 2021 19:03:32 +0200
Subject: [PATCH] Add Qt 6 and drop Qt 4 from CMakeLists

---
 CMakeLists.txt       | 157 ++++++++++++++++++++++++++-----------------
 src/CMakeLists.txt   |  13 ++--
 tests/CMakeLists.txt |  28 ++++----
 3 files changed, 111 insertions(+), 87 deletions(-)

diff --git a/CMakeLists.txt b/CMakeLists.txt
index 1a8c8e3..9cfc5c8 100644
--- a/CMakeLists.txt
+++ b/CMakeLists.txt
@@ -3,48 +3,99 @@ cmake_minimum_required( VERSION 3.0 FATAL_ERROR )
 project( libmygpo-qt )
 
 set( CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} ${CMAKE_CURRENT_SOURCE_DIR}/cmake/modules)
-set( CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -DQT_NO_CAST_FROM_ASCII -DQT_NO_CAST_TO_ASCII" )
 
-option(BUILD_WITH_QT4 "Build libmygpo-qt with Qt4" OFF)
-option(MYGPO_BUILD_TESTS "Build all unit tests" ON)
+include(CheckCXXCompilerFlag)
+
+list(APPEND COMPILE_OPTIONS
+  -Wall
+  -Wextra
+  -Wpedantic
+  -Wunused
+  -Wshadow
+  -Wundef
+  -Wpacked
+  -Wuninitialized
+  -Wredundant-decls
+  -Wunused-parameter
+  -Wcast-align
+  -Wchar-subscripts
+  -Wpointer-arith
+  -Wwrite-strings
+  -Wformat-security
+  -Wold-style-cast
+  -Woverloaded-virtual
+  -Wnon-virtual-dtor
+  -Wstack-protector
+  -Winit-self
+  -Wmissing-format-attribute
+  -Wmissing-include-dirs
+  -Wmissing-declarations
+  -Wstrict-overflow=2
+  -Wformat=2
+  -Wdisabled-optimization
+  -fno-inline
+)
+if ( NOT WIN32 )
+  list(APPEND COMPILE_OPTIONS -fvisibility=hidden )
+endif()
+
+if (CMAKE_COMPILER_IS_GNUCXX)
+  list(APPEND COMPILE_OPTIONS
+    -ggdb3
+    -Wunsafe-loop-optimizations
+  )
+  check_cxx_compiler_flag( -Wlogical-op GNUCXX_HAS_WLOGICAL_OP )
+  if ( GNUCXX_HAS_WLOGICAL_OP )
+    list(APPEND COMPILE_OPTIONS -Wlogical-op )
+  endif()
+endif (CMAKE_COMPILER_IS_GNUCXX)
+
+add_compile_options(${COMPILE_OPTIONS})
+
+add_definitions(-DQT_STRICT_ITERATOR -DQT_NO_CAST_FROM_ASCII -DQT_NO_CAST_TO_ASCII)
+
+option( MYGPO_QT_MAJOR_VERSION "Qt version to use (5 or 6)" )
+option( BUILD_WITH_QT5 "Build libmygpo-qt with Qt 5" OFF )
+option( BUILD_WITH_QT6 "Build libmygpo-qt with Qt 6" OFF )
+option( MYGPO_BUILD_TESTS "Build all unit tests" ON )
+
+set(QT_COMPONENTS Core Network)
+if(MYGPO_BUILD_TESTS)
+    list(APPEND QT_COMPONENTS Test)
+endif()
 
-if( NOT BUILD_WITH_QT4 )
-    if( MYGPO_BUILD_TESTS )
-        find_package(Qt5 REQUIRED COMPONENTS Core Network Test CONFIG)
+if( BUILD_WITH_QT6 OR MYGPO_QT_MAJOR_VERSION EQUAL 6 )
+    set( MYGPO_QT_MAJOR_VERSION 6 CACHE STRING "" FORCE )
+    set( BUILD_WITH_QT6 ON CACHE BOOL "" FORCE )
+elseif( BUILD_WITH_QT5 OR MYGPO_QT_MAJOR_VERSION EQUAL 5 )
+    set( MYGPO_QT_MAJOR_VERSION 5 CACHE STRING "" FORCE )
+    set( BUILD_WITH_QT5 ON CACHE BOOL "" FORCE )
+    set( QT_MIN_VERSION 5.2 )
+ else()
+    # Automatically detect Qt version.
+    find_package( QT NAMES Qt6 Qt5 COMPONENTS ${QT_COMPONENTS} REQUIRED )
+    if( QT_FOUND AND QT_VERSION_MAJOR EQUAL 6 )
+        set( BUILD_WITH_QT6 ON CACHE BOOL "" FORCE )
+        set( MYGPO_QT_MAJOR_VERSION 6 CACHE STRING "" FORCE )
+    elseif (QT_FOUND AND QT_VERSION_MAJOR EQUAL 5 )
+        set( BUILD_WITH_QT5 ON CACHE BOOL "" FORCE )
+        set( MYGPO_QT_MAJOR_VERSION 5 CACHE STRING "" FORCE )
+        set( QT_MIN_VERSION 5.2 )
     else()
-        find_package(Qt5 REQUIRED COMPONENTS Core Network CONFIG)
+        message( FATAL_ERROR "Could not find a suitable Qt version." )
     endif()
-    set(MYGPO_QT_VERSION_SUFFIX 5)
-
-    macro(qt_wrap_cpp)
-        qt5_wrap_cpp(${ARGN})
-    endmacro()
-
-    # pkg-config names of QtCore and QtNetwork are Qt5Core and Qt5Network for
-    # Qt5
-    set(MYGPO_QT_MAJOR_VERSION "5")
-    set(MYGPO_QT4_QJSON_DEP "")
-else()
-    message(STATUS "Could not find Qt5, searching for Qt4 instead...")
-    message(STATUS "Qt4 Support is deprecated, building with Qt4 is no longer officially supported")
-    if( MYGPO_BUILD_TESTS )
-        find_package( Qt4 COMPONENTS QtCore QtNetwork QtTest REQUIRED )
-    else( MYGPO_BUILD_TESTS )
-        find_package( Qt4 COMPONENTS QtCore QtNetwork REQUIRED )
-    endif()
-    set( QT_DONT_USE_QTGUI TRUE )
-    include( ${QT_USE_FILE} )
+endif()
 
-    macro(qt_wrap_cpp)
-        qt4_wrap_cpp(${ARGN})
-    endmacro()
+if( BUILD_WITH_QT5 AND Qt5Core_VERSION VERSION_LESS 5.15.0 )
+   macro(qt_wrap_cpp)
+       qt5_wrap_cpp(${ARGN})
+   endmacro()
+endif()
 
-    set(MYGPO_QT_MAJOR_VERSION "")
-    set(MYGPO_QT4_QJSON_DEP "Requires.private: QJson")
+find_package( Qt${MYGPO_QT_MAJOR_VERSION} ${QT_MIN_VERSION} REQUIRED COMPONENTS ${QT_COMPONENTS} )
 
-    # QJson is only required for Qt4
-    find_package(QJSON REQUIRED)
-endif()
+set( MYGPO_QT_VERSION_SUFFIX ${MYGPO_QT_MAJOR_VERSION} )
+set( QT_DEFAULT_MAJOR_VERSION ${MYGPO_QT_MAJOR_VERSION} )
 
 # Don't use absolute path in Mygpo-qtTargets-$buildType.cmake
 # (This will have no effect with CMake < 2.8)
@@ -60,24 +111,6 @@ set( MYGPO_QT_SONAME "${MYGPO_QT_VERSION_MAJOR}")
 #Configure Version.h.in with the actual version number
 configure_file( src/Version.h.in ${CMAKE_CURRENT_BINARY_DIR}/src/Version.h )
 
-include(CheckCXXCompilerFlag)
-
-if (CMAKE_COMPILER_IS_GNUCXX)
-    ADD_DEFINITIONS( -Wcast-align -Wchar-subscripts -Wpointer-arith
-       -Wwrite-strings -Wpacked -Wformat-security -Wmissing-format-attribute
-       -Wold-style-cast -Woverloaded-virtual -Wnon-virtual-dtor  -Wall -Wextra
-       -Wformat=2 -Wundef -Wstack-protector -Wmissing-include-dirs
-       -Winit-self -Wunsafe-loop-optimizations  -ggdb3 -fno-inline -DQT_STRICT_ITERATORS )
-    if ( NOT WIN32 )
-        add_definitions( -fvisibility=hidden )
-    endif()
-
-    check_cxx_compiler_flag( -Wlogical-op GNUCXX_HAS_WLOGICAL_OP )
-    if ( GNUCXX_HAS_WLOGICAL_OP )
-        add_definitions( -Wlogical-op )
-    endif()
-endif(CMAKE_COMPILER_IS_GNUCXX)
-
 if(MYGPO_BUILD_TESTS)
     INCLUDE(CTest)
     enable_testing()
@@ -116,24 +149,24 @@ if(DOXYGEN_FOUND)
 endif(DOXYGEN_FOUND)
 
 # make uninstall support
-CONFIGURE_FILE(
+configure_file(
   "${CMAKE_CURRENT_SOURCE_DIR}/cmake_uninstall.cmake.in"
   "${CMAKE_CURRENT_BINARY_DIR}/cmake_uninstall.cmake"
   IMMEDIATE @ONLY)
 
-ADD_CUSTOM_TARGET(uninstall
+add_custom_target(uninstall
   "${CMAKE_COMMAND}" -P "${CMAKE_CURRENT_BINARY_DIR}/cmake_uninstall.cmake")
 
 # pkg-config
-IF (NOT WIN32)
-    FIND_PACKAGE(PkgConfig)
-    IF (PKG_CONFIG_FOUND)
-        CONFIGURE_FILE (${CMAKE_CURRENT_SOURCE_DIR}/libmygpo-qt.pc.in
+if (NOT WIN32)
+    find_package(PkgConfig)
+    if (PKG_CONFIG_FOUND)
+        configure_file (${CMAKE_CURRENT_SOURCE_DIR}/libmygpo-qt.pc.in
                             ${CMAKE_CURRENT_BINARY_DIR}/libmygpo-qt${MYGPO_QT_VERSION_SUFFIX}.pc
                             @ONLY)
-         INSTALL (FILES ${CMAKE_CURRENT_BINARY_DIR}/libmygpo-qt${MYGPO_QT_VERSION_SUFFIX}.pc DESTINATION lib${LIB_SUFFIX}/pkgconfig)
-    ENDIF (PKG_CONFIG_FOUND)
-ENDIF (NOT WIN32)
+         install (FILES ${CMAKE_CURRENT_BINARY_DIR}/libmygpo-qt${MYGPO_QT_VERSION_SUFFIX}.pc DESTINATION lib${LIB_SUFFIX}/pkgconfig)
+    endif (PKG_CONFIG_FOUND)
+endif (NOT WIN32)
 
 set(MYGPO_QT_TARGET_NAME mygpo-qt${MYGPO_QT_VERSION_SUFFIX} CACHE INTERNAL "" FORCE )
 
diff --git a/src/CMakeLists.txt b/src/CMakeLists.txt
index 253e81e..95a9c15 100644
--- a/src/CMakeLists.txt
+++ b/src/CMakeLists.txt
@@ -1,6 +1,3 @@
-
-include_directories( ${QJSON_INCLUDE_DIR} ${CMAKE_CURRENT_SOURCE_DIR} ${CMAKE_CURRENT_BINARY_DIR} )
-
 set ( LIBMYGPO_QT_SRC
         Config.cpp
         DeviceSyncResult.cpp
@@ -75,17 +72,15 @@ set ( LIBMYGPO_QT_INSTALL_H
         AddRemoveResult.h
 )
 
-QT_WRAP_CPP(LIBMYGPO_QT_MOC_SRC ${LIBMYGPO_QT_MOC_H} )
+qt_wrap_cpp(LIBMYGPO_QT_MOC_SRC ${LIBMYGPO_QT_MOC_H} )
 
 add_library( ${MYGPO_QT_TARGET_NAME} SHARED ${LIBMYGPO_QT_SRC} ${LIBMYGPO_QT_MOC_SRC} )
 
 set_target_properties( ${MYGPO_QT_TARGET_NAME} PROPERTIES VERSION ${MYGPO_QT_VERSION} SOVERSION ${MYGPO_QT_SONAME} DEFINE_SYMBOL MYGPO_MAKEDLL)
 
-if( NOT BUILD_WITH_QT4 )
-    target_link_libraries( ${MYGPO_QT_TARGET_NAME} Qt5::Core Qt5::Network)
-else()
-    target_link_libraries( ${MYGPO_QT_TARGET_NAME} ${QT_QTCORE_LIBRARY} ${QT_QTNETWORK_LIBRARY} ${QJSON_LIBRARIES})
-endif()
+target_include_directories( ${MYGPO_QT_TARGET_NAME} PRIVATE ${CMAKE_CURRENT_SOURCE_DIR} ${CMAKE_CURRENT_BINARY_DIR} )
+
+target_link_libraries( ${MYGPO_QT_TARGET_NAME} PRIVATE Qt${MYGPO_QT_VERSION_SUFFIX}::Core Qt${MYGPO_QT_VERSION_SUFFIX}::Network)
 
 install( TARGETS ${MYGPO_QT_TARGET_NAME} EXPORT ${MYGPO_QT_TARGET_NAME}Export DESTINATION ${LIB_INSTALL_DIR} )
 
diff --git a/tests/CMakeLists.txt b/tests/CMakeLists.txt
index 4f77158..4bed6d7 100644
--- a/tests/CMakeLists.txt
+++ b/tests/CMakeLists.txt
@@ -7,35 +7,31 @@ remove_definitions(-DQT_NO_CAST_TO_ASCII)
 
 set ( TESTOBJECTS_SRCDIR ${CMAKE_CURRENT_SOURCE_DIR}/../src )
 
-include_directories( ${QJSON_INCLUDE_DIR} ${CMAKE_CURRENT_BINARY_DIR} ${CMAKE_CURRENT_BINARY_DIR}/../src ${TESTOBJECTS_SRCDIR} )
-
 # UrlBuilder library
 add_library( UrlBuilder ${TESTOBJECTS_SRCDIR}/UrlBuilder.cpp ${TESTOBJECTS_SRCDIR}/Config.cpp )
-if( NOT BUILD_WITH_QT4 )
-    target_link_libraries( UrlBuilder Qt5::Core )
-endif()
+target_include_directories( UrlBuilder PRIVATE ${CMAKE_CURRENT_BINARY_DIR} ${CMAKE_CURRENT_BINARY_DIR}/../src ${TESTOBJECTS_SRCDIR} )
+target_link_libraries( UrlBuilder PRIVATE Qt${MYGPO_QT_VERSION_SUFFIX}::Core )
+
 # JSONCreator library
-QT_WRAP_CPP(LIBMYGPO_QT_MOC_SRC ${TESTOBJECTS_SRCDIR}/EpisodeAction.h ${TESTOBJECTS_SRCDIR}/EpisodeAction_p.h )
+qt_wrap_cpp(LIBMYGPO_QT_MOC_SRC ${TESTOBJECTS_SRCDIR}/EpisodeAction.h ${TESTOBJECTS_SRCDIR}/EpisodeAction_p.h )
 add_library( JsonCreator ${TESTOBJECTS_SRCDIR}/JsonCreator.cpp ${TESTOBJECTS_SRCDIR}/EpisodeAction.cpp ${TESTOBJECTS_SRCDIR}/qjsonwrapper/Json.cpp ${LIBMYGPO_QT_MOC_SRC})
-if( NOT BUILD_WITH_QT4 )
-    target_link_libraries( JsonCreator Qt5::Core Qt5::Network )
-endif()
+target_include_directories( JsonCreator PRIVATE ${CMAKE_CURRENT_BINARY_DIR} ${CMAKE_CURRENT_BINARY_DIR}/../src ${TESTOBJECTS_SRCDIR} )
+target_link_libraries( JsonCreator PRIVATE Qt${MYGPO_QT_VERSION_SUFFIX}::Core Qt${MYGPO_QT_VERSION_SUFFIX}::Network )
 
 set( EXECUTABLE_OUTPUT_PATH ${CMAKE_CURRENT_BINARY_DIR} )
 
 macro(add_libmygpo_test _source)
     get_filename_component(_name ${_source} NAME_WE)
-    QT_WRAP_CPP(${_name}_MOC_SRC ${_name}.h)
+    qt_wrap_cpp(${_name}_MOC_SRC ${_name}.h)
     add_executable(${_name} ${_source} ${${_name}_MOC_SRC})
-    target_link_libraries(${_name} ${QT_QTCORE_LIBRARY} ${QT_QTTEST_LIBRARY} ${QT_QTNETWORK_LIBRARY})
+    target_include_directories( ${_name} PRIVATE ${CMAKE_CURRENT_BINARY_DIR} ${CMAKE_CURRENT_BINARY_DIR}/../src ${TESTOBJECTS_SRCDIR} )
+    target_link_libraries(${_name} PRIVATE ${QT_QTCORE_LIBRARY} ${QT_QTTEST_LIBRARY} ${QT_QTNETWORK_LIBRARY})
     add_test(${_name}-test ${EXECUTABLE_OUTPUT_PATH}/${_name})
-    if( NOT BUILD_WITH_QT4 )
-        target_link_libraries(${_name} Qt5::Network Qt5::Test)
-    endif()
+    target_link_libraries(${_name} PRIVATE Qt${MYGPO_QT_VERSION_SUFFIX}::Network Qt${MYGPO_QT_VERSION_SUFFIX}::Test)
 endmacro(add_libmygpo_test)
 
 add_libmygpo_test( UrlBuilderTest.cpp )
-target_link_libraries( UrlBuilderTest UrlBuilder)
+target_link_libraries( UrlBuilderTest PRIVATE UrlBuilder)
 
 add_libmygpo_test( JsonCreatorTest.cpp )
-target_link_libraries( JsonCreatorTest JsonCreator ${QJSON_LIBRARIES} )
+target_link_libraries( JsonCreatorTest PRIVATE JsonCreator )
